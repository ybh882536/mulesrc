import org.mule.gradle.ContentsVerifierTask

project.ext {
    dummyProjects = [ project(":modules"), project(":transports"), project(":tests") ]
}

allprojects {
    apply plugin: 'maven'

    group = 'org.mule'
    version = '4.0-SNAPSHOT'

    repositories {
        mavenLocal()
        maven { url "https://repository.mulesoft.org/nexus/content/repositories/public/" }
    }
}

subprojects {
    apply plugin: 'java'


    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    tasks.withType(JavaCompile) {
        options.encoding = 'ISO-8859-1'
//        options.incremental = true
    }

    configurations {
        log
        testFixtures {
           extendsFrom testRuntime
        }

        all {
            resolutionStrategy {
                force 'xalan:xalan:2.7.2'

                eachDependency { details ->
                    if (details.requested.group == 'org.springframework') {
                        details.useVersion springVersion
                    }

                }
            }
        }

        all*.exclude module: 'slf4j-jdk14'
        all*.exclude group:'org.eclipse.jetty.orbit'
        all*.exclude group:'xerces'
    }


    dependencies {
    log "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "org.slf4j:slf4j-api:$slf4jVersion",
            'org.apache.logging.log4j:log4j-api:2.0.2',
            'org.apache.logging.log4j:log4j-1.2-api:2.0.2',
            'org.apache.logging.log4j:log4j-core:2.0.2',
            'org.apache.logging.log4j:log4j-jcl:2.0.2',
            'org.apache.logging.log4j:log4j-slf4j-impl:2.0.2'

//    compile configurations.log
    testCompile 'com.googlecode.multithreadedtc:multithreadedtc:1.01',
            'junit:junit:4.11',
            'org.mockito:mockito-core:1.9.0',
            'xmlunit:xmlunit:1.1'
    }
}

// Used in specific projects that need to export test classes
def exportTestFixture(project) {
    project.tasks.create(name: 'testJar', type: Jar) {
         from project.sourceSets.test.output
         classifier = 'test'
    }

    project.artifacts {
         testFixtures project.testJar
    }
}

// Used in specific project that need slightly different configurations based on the
// JDK they are compiled with
def perJdkSources(project) {
    project.sourceSets {
        jdk6 { java { srcDir "src/main/jdk6" } }
        testJdk6 { java { srcDir "src/test/jdk6" }}
        jdk7 { java { srcDir "src/main/jdk7" } }
        testJdk7 { java { srcDir "src/test/jdk7" }}

    }

    def version = System.getProperty("java.version")
    if (version.startsWith("1.6")) {
        project.compileJava.doFirst {
            println "Using JDK6 specific code"
        }
        project.compileJava.source project.sourceSets.jdk6.java
        project.compileTestJava.source project.sourceSets.testJdk6.java
    }
    else {
        project.compileJava.doFirst {
            println "Using JDK7 specific code"
        }
        project.compileJava.source project.sourceSets.jdk7.java
        project.compileTestJava.source project.sourceSets.testJdk7.java
    }
}

configure(dummyProjects) {

}

// Distributions: built directly from the top level project
apply plugin: 'distribution'

configurations {
    allMule

    libBoot
    libBoot.exclude module:'core'
}

dependencies {
    allMule project(':core')
    allMule project(':tests:functional')
    allMule project(':modules').subprojects
    allMule project(':transports').subprojects

    libBoot project(':modules:boot')
    libBoot project(':modules:reboot')
    libBoot project(':modules:logging')
}

def realProjects = subprojects - dummyProjects


distributions {
    embedded {
        baseName = 'embedded'
        contents {
            into('') {
                from "src/dist/standalone"
            }
            into('mule') {
                from configurations.allMule
                include "mule*.jar"
            }
            into('deps') {
                from configurations.allMule
                exclude "mule*.jar"
            }

        }
    }

    main {
        baseName = 'standalone'
        contents {
            def muleMask = ["mule*.jar"]
            def userMask = ["junit*.jar", "xmlunit*.jar", "mule-tests-functional*.jar"]
            def endorsedMask = ["xalan*.jar", "xml-apis*.jar", "xercesImpl*.jar", "xml-serializer*.jar"]

            into('') {
                //from "src/dist/standalone"
                from "distributions/standalone/src/main/resources"
            }
            into('lib/boot') {
                from configurations.libBoot
            }

            into('lib/mule') {
                from configurations.allMule - configurations.libBoot
                include muleMask
                exclude userMask
            }
            into('lib/user') {
                from configurations.allMule
                include userMask
            }
            into('lib/endorsed') {
                from configurations.allMule
                include endorsedMask
            }

            into('lib/opt') {
                from configurations.allMule
                exclude userMask
                exclude muleMask
                exclude endorsedMask
            }

        }
    }
}

task verify(type: ContentsVerifierTask) {
    whitelist= file("whitelist.test")
    projectOutputFolder = file("$buildDir/tmp")
}

//distTar.dependsOn subProjects.assemble